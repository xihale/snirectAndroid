plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

// 动态从 Git 获取版本号 (使用 commit 总数)
def getGitVersionCode(project) {
    try {
        def count = project.providers.exec {
            workingDir project.rootProject.projectDir.parentFile
            commandLine 'git', 'rev-list', '--count', 'HEAD'
        }.standardOutput.asText.get().trim()
        return count.toInteger()
    } catch (Exception e) {
        return 1
    }
}

// 动态从 Git Tag 获取版本名
def getGitVersionName(project) {
    try {
        return project.providers.exec {
            workingDir project.rootProject.projectDir.parentFile
            commandLine 'bash', 'git_version.sh'
        }.standardOutput.asText.get().trim()
    } catch (Exception e) {
        return "0.0.0-fallback"
    }
}

android {
    namespace 'com.xihale.snirect'
    compileSdk 36

    defaultConfig {
        applicationId "com.xihale.snirect"
        minSdk 26
        targetSdk 36
        versionCode getGitVersionCode(project)
        
        versionName getGitVersionName(project)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        resConfigs "zh", "en"

        ndk {
            abiFilters 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            ndk {
                debugSymbolLevel = 'none'
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            // 在 debug 模式保持默认配置以方便调试，不建议开启混淆
            minifyEnabled false
            shrinkResources false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.10.0'
    implementation 'androidx.activity:activity-compose:1.12.2'
    
    implementation platform('androidx.compose:compose-bom:2026.01.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material-icons-core'
    
    implementation 'androidx.navigation:navigation-compose:2.9.7'
    implementation 'androidx.datastore:datastore-preferences:1.2.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.10.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.akuleshov7:ktoml-core:0.7.1'
    implementation fileTree(dir: 'libs', include: ['*.aar'])

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
}